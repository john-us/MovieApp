plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}
apply from: '../dependencies.gradle'
android {
    namespace rootProject.ext.app_config.applicationId
    compileSdk rootProject.ext.app_config.compileSdkVersion

    defaultConfig {
        applicationId rootProject.ext.app_config.applicationId
        minSdk rootProject.ext.app_config.minSdkVersion
        targetSdk rootProject.ext.app_config.targetSdkVersion
        versionCode rootProject.ext.app_config.versionCode
        versionName rootProject.ext.app_config.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


    flavorDimensions.add("environment")
    productFlavors {
        dev {
            dimension "environment"
            applicationId "com.movieapp.dev"
            buildConfigField "String", "BASE_URL", '"https://api.themoviedb.org/3/"'
        }

        prod {
            dimension "environment"
            applicationId "com.movieapp"
            buildConfigField "String", "BASE_URL", '"https://api.themoviedb.org/3/"'
        }
    }
    compileOptions {
        sourceCompatibility rootProject.ext.app_config.javaVersion
        targetCompatibility rootProject.ext.app_config.javaVersion
    }
    kotlinOptions {
        jvmTarget = rootProject.ext.app_config.jvmTarget
    }
    buildFeatures {
        buildConfig = true
    }
}

dependencies {
    implementation rootProject.ext.dependencies.hilt
    kapt rootProject.ext.dependencies.hiltCompiler
    implementation rootProject.ext.dependencies.lifecycleExtension

    //common module
    implementation project(path: ':common')
    //presentation module
    implementation project(path: ':features:movie:presentation')
    //data module
    implementation project(path: ':features:movie:data')
    //domain module
    implementation project(path: ':features:movie:domain')
    implementation project(path: ':network')
}
// Allow references to generated code
kapt {
    correctErrorTypes true
}